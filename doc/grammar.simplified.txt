
RMM ::=
    BAD-STATES PREDICATES 'data' VAR-INIT PROCESS-LIST
  | BAD-STATES PREDICATES PROCESS-LIST

BAD-STATES ::= 'forbidden' BAD-STATES-LIST-LIST

BAD-STATES-LIST-LIST ::= 
    BAD-STATES-LIST
  | BAD-STATES-LIST ';' BAD-STATES-LIST-LIST

BAD-STATES-LIST ::=
    LABEL
  | LABEL BAD-STATES-LIST

PREDICATES ::=
    ''
  | 'predicates' BEXPR-LIST

BEXPR-LIST ::=
    BEXPR
  | BEXPR ';' BEXPR-LIST

PROCESS-LIST ::=
    PROCESS
  | PROCESS PROCESS-LIST

PROCESS ::=
  'process' PROC-COUNT VAR-INIT REG-INIT 'text' STMT-LIST

PROC-COUNT ::=
    ''
  | '(' NAT ')'

VAR-INIT ::=
    ''
  | 'data' VAR-INIT-LIST

VAR-INIT-LIST ::=
    ID '=' VAR-INIT-VALUE VAR-DOMAIN
  | ID '=' VAR-INIT-VALUE VAR-DOMAIN ',' VAR-INIT-LIST

VAR-INIT-VALUE ::= 
    NAT
  | '-' NAT
  | '*'  

VAR-DOMAIN ::=
    ''
  | ':' '[' INT ':' INT ']'
  | ':' 'Z'

REG-INIT ::=
    ''
  | 'registers' REG-INIT-LIST

REG-INIT-LIST ::=
    REG '=' VAR-INIT-VALUE VAR-DOMAIN
  | REG '=' VAR-INIT-VALUE VAR-DOMAIN ',' REG-INIT-LIST

STMT-LIST ::=
    LSTMT
  | LSTMT ';' STMT-LIST

LSTMT ::=
    STMT
  | LABEL ':' STMT

STMT ::=
    'nop'
  | 'read:' MEMLOC '=' EXPR
  | 'read:' REG ':=' MEMLOC
  | 'write:' MEMLOC ':=' EXPR
  | 'locked write:' MEMLOC ':=' EXPR
  | 'cas(' MEMLOC ',' EXPR ',' EXPR ')'
  | REG ':=' EXPR
  | 'assume:' BEXPR
  | 'if' BEXPR 'then' LSTMT
  | 'if' BEXPR 'then' LSTMT 'else' LSTMT
  | 'while' BEXPR 'do' LSTMT
  | 'goto' LABEL
  | 'either' '{' STMT-LIST EITHER-LIST '}'
  | 'locked' '{' STMT-LIST EITHER-LIST '}'
  | '{' STMT-LIST '}'

EITHER-LIST ::=
    ''
  | 'or' STMT-LIST EITHER-LIST

BEXPR ::=
    BEXPR-AND
  | BEXPR '||' BEXPR

BEXPR-AND ::=
    BEXPR-ATOM
  | BEXPR-AND '&&' BEXPR-AND
  | 'not' BEXPR-ATOM

BEXPR-ATOM ::=
    'true'
  | 'false'
  | EXPR '=' EXPR
  | EXPR '!=' EXPR
  | EXPR '<' EXPR
  | EXPR '>' EXPR
  | '[' BEXPR ']'

EXPR ::=
    EXPR '+' EXPR
  | EXPR '-' EXPR-UNIT
  | EXPR-UNIT

EXPR-UNIT ::=
    REG
  | NAT
  | '-' EXPR-UNIT
  | '(' EXPR ')'

REG ::= $[_a-zA-Z0-9]+

MEMLOC ::=
    ID
  | ID '[' 'my' ']'
  | ID '[' NAT ']'
  | '[' EXPR ']'

LABEL ::= ID

ID ::= [_a-zA-Z][_a-zA-Z0-9]*      

